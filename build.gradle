buildscript {
    ext.kotlin_version = '1.2.61'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name 'forge'
            url 'http://files.minecraftforge.net/maven/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'flavor.pie.promptsign' version '1.1.0'
    id 'java'
}

group 'flavor.pie'
version '0.6.1-SNAPSHOT'

apply plugin: 'kotlin'
//apply plugin: 'kotlin-kapt'
apply plugin: 'net.minecraftforge.gradle.forge'

repositories {
    mavenCentral()
    maven {
        url 'https://repo.spongepowered.org/maven/'
    }
    maven {
        url 'https://jitpack.io/'
    }
    maven {
        url 'https://repo.codemc.org/repository/maven-public'
    }
}

configurations {
    compile.extendsFrom shadow
//    compile.extendsFrom kapt
}

dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'org.spongepowered:spongeapi:7.1.0'
//    kapt 'org.spongepowered:spongeapi:7.0.0'
    shadow 'com.github.pie-flavor:Kludge:caa38e9'
    shadow 'com.github.pie-flavor:MoreArguments:d85d7c7'
    shadow 'org.bstats:bstats-sponge-lite:1.4'
}

jar.enabled = false
build.dependsOn shadowJar
signArchives.dependsOn shadowJar
reobfJar.dependsOn shadowJar

shadowJar {
    classifier = null
    configurations = [project.configurations.shadow]
    relocate('kotlin', 'flavor.pie.pieconomy.runtime.kotlin')
    relocate('flavor.pie.kludge', 'flavor.pie.pieconomy.util.kludge')
    relocate('flavor.pie.util.arguments', 'flavor.pie.pieconomy.util.arguments')
    relocate('org.bstats', 'flavor.pie.pieconomy.metrics')
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

minecraft {
    forgeVersion = '2705'
    mappings = 'stable_39'
}
